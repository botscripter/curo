plugins {
    id "idea"
    id "java-library"
    id "maven-publish"
    id "org.jetbrains.kotlin.jvm" version "1.4.21-2"
    id "org.jetbrains.kotlin.plugin.jpa" version "1.4.21-2"
    id "org.springframework.boot" version "2.4.2"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "org.jetbrains.kotlin.kapt" version "1.4.21-2"
}

group = "ch.umb.curo"
version = "0.0.10"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

tasks.bootJar {enabled = false}
tasks.jar {enabled = true}

jar {
    baseName 'curo-spring-boot-starter'
}

ext {
    camunda_version = "7.14.0"
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.camunda.bpm:camunda-bom:$camunda_version"
    }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2'
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"

    kapt "org.springframework.boot:spring-boot-configuration-processor"

    implementation "com.fasterxml.jackson.datatype:jackson-datatype-joda:2.4.2"

    compileOnly "org.camunda.bpm.springboot:camunda-bpm-spring-boot-starter:7.14.0"
    implementation "org.camunda.bpm.springboot:camunda-bpm-spring-boot-starter-rest:7.14.0"
    implementation "org.camunda.spin:camunda-spin-core"
    implementation "org.camunda.spin:camunda-spin-dataformat-json-jackson"
    implementation "org.camunda.bpm:camunda-engine-plugin-spin"

    implementation "org.springdoc:springdoc-openapi-ui:1.5.3"
    implementation 'org.springdoc:springdoc-openapi-kotlin:1.5.2'

    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.auth0:java-jwt:3.10.3"
    implementation 'com.auth0:jwks-rsa:0.15.0'

    implementation "org.zalando:logbook-logstash:2.3.0"
    implementation "org.zalando:logbook-spring-boot-starter:2.2.0"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.boot:spring-boot-starter-web"
    testImplementation "org.camunda.bpm.springboot:camunda-bpm-spring-boot-starter-webapp:7.14.0"
    testImplementation "com.h2database:h2"

}

test {
    useJUnitPlatform()
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "11"
    }
}

publishing {
    repositories {
        maven {
            def releaseRepo = "https://repo.umb.cloud/repository/maven-curo-releases" //TODO: release to new nexus
            def snapshotRepo = "" //TODO: release to new nexus
            url = version.endsWith('SNAPSHOT') ? snapshotRepo : releaseRepo

            credentials {
                username = project.findProperty("nexusUser") ?: System.getenv("UMB_NEXUS_USER")
                password = project.findProperty("nexusPassword")  ?: System.getenv("UMB_NEXUS_PASSWORD")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            version = project.version
            artifactId = 'curo-spring-boot-starter'

            artifact jar

            pom {
                name = "curo-spring-boot-starter"
            }
        }
    }
}
